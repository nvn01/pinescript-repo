// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RickAtw

//@version=3

study(title="T3 Gold Sniper RickAtw", overlay=true )

typeColoured   = input(defval="TMA", title="Coloured MA Type: ", options=["SMA", "EMA", "WMA", "VWMA", "SMMA", "DEMA", "TEMA", "HullMA", "ZEMA", "TMA", "SSMA"], type=string)
lenColoured    = input(defval=18, title="Coloured MA - Length", minval=1)
srcColoured    = input(close,title="Coloured MA - Source")
// Fast MA - type, length
typeFast       = input(defval="EMA", title="Fast MA Type: ", options=["SMA", "EMA", "WMA", "VWMA", "SMMA", "DEMA", "TEMA", "HullMA", "ZEMA", "TMA", "SSMA"], type=string)
lenFast        = input(defval=21, title="Fast MA - Length", minval=1)
// Medium MA - type, length
typeMedium     = input(defval="EMA", title="Medium MA Type: ", options=["SMA", "EMA", "WMA", "VWMA", "SMMA", "DEMA", "TEMA", "HullMA", "ZEMA", "TMA", "SSMA"], type=string)
lenMedium      = input(defval=55, title="Medium MA - Length", minval=1)
// Slow MA - type, length
typeSlow       = input(defval="EMA", title="Slow MA Type: ", options=["SMA", "EMA", "WMA", "VWMA", "SMMA", "DEMA", "TEMA", "HullMA", "ZEMA", "TMA", "SSMA"], type=string)
lenSlow        = input(defval=89, title="Slow MA Length", minval=1)
// 3xMA source
ma_src         = input(close,title="3xMA and Bollinger Source")

//
filterOption        = input("SuperTrend+3xMA",title="Signal Filter Option : ",
      options=["3xMATrend","SuperTrend","SuperTrend+3xMA","ColouredMA","No Alerts","MACross","MACross+ST","MACross+3xMA",
               "OutsideIn:MACross","OutsideIn:MACross+ST","OutsideIn:MACross+3xMA"])
//
hideMALines        = input(false)
hideSuperTrend     = input(true)
hideTrendDirection = input(true)      
//
disableFastMAFilter   = input(false)
disableMediumMAFilter = input(false)
disableSlowMAFilter   = input(false)
//
uKC             = false 

oiLength        = input(8, title="Bollinger Outside In LookBack")
//
SFactor         = input(3.618, minval=1.0, title="SuperTrend Factor")
SPd             = input(5, minval=1, title="SuperTrend Length")
//
buyColour_      = input("Green", title="BUY Marker Colour: ",options=["Green","Lime","Aqua","DodgerBlue","Gray","Yellow"])
sellColour_     = input("Maroon",title="SELL Marker Colour: ",options=[ "Maroon","Red","Fuchsia","Blue","Black","Orange"])

uSuperTrendFilter   = filterOption == "SuperTrend" ? true : false
u3xMATrendFilter    = filterOption == "3xMATrend"? true : false
uBothTrendFilters   = filterOption == "SuperTrend+3xMA"? true : false
uOIMACrossFilter    = filterOption == "OutsideIn:MACross" ? true : false
uOI3xMAFilter       = filterOption == "OutsideIn:MACross+3xMA" ? true : false
uOISTFilter         = filterOption == "OutsideIn:MACross+ST" ? true : false
uMACrossFilter      = filterOption == "MACross" ? true : false
uMACrossSTFilter    = filterOption == "MACross+ST" ? true : false
uMACross3xMAFilter  = filterOption == "MACross+3xMA" ? true : false
disable3xMAFilter   = disableFastMAFilter and disableMediumMAFilter and disableSlowMAFilter
u3xMATrendFilter    := disable3xMAFilter? false : u3xMATrendFilter
disableAllFilters   = u3xMATrendFilter or uSuperTrendFilter or uBothTrendFilters or uOI3xMAFilter or uOISTFilter or  
                      uOIMACrossFilter or uMACrossFilter or uMACrossSTFilter or uMACross3xMAFilter? false : true
disableAllFilters   := filterOption == "No Alerts"? false : disableAllFilters
uSuperTrendFilter   := filterOption == "No Alerts"? false : uSuperTrendFilter
u3xMATrendFilter    := filterOption == "No Alerts"? false : u3xMATrendFilter
uBothTrendFilters   := filterOption == "No Alerts"? false : uBothTrendFilters
uOIMACrossFilter    := filterOption == "No Alerts"? false : uOIMACrossFilter
uOI3xMAFilter       := filterOption == "No Alerts"? false : uOI3xMAFilter
uOISTFilter         := filterOption == "No Alerts"? false : uOISTFilter
uMACrossFilter      := filterOption == "No Alerts"? false : uMACrossFilter
uMACrossSTFilter    := filterOption == "No Alerts"? false : uMACrossSTFilter
uMACross3xMAFilter  := filterOption == "No Alerts"? false : uMACross3xMAFilter
dodgerblue = #1E90FF
lightcoral = #F08080
buyColour  = green  
sellColour = #f44336

buyclr = buyColour_=="Lime"?lime: buyColour_=="Aqua"?aqua: buyColour_=="DodgerBlue"?dodgerblue: buyColour_=="Gray"?gray: buyColour_=="Yellow"?yellow:green
sellclr = sellColour_=="Red"?red: sellColour_=="Fuchsia"?fuchsia: sellColour_=="Blue"?blue: sellColour_=="Black"?black: sellColour_=="Orange"?orange: maroon

variant(type, src, len) =>
    v1 = sma(src, len)                                               
    v2 = ema(src, len)                                                
    v3 = wma(src, len)                                                 
    v4 = vwma(src, len)                                               
    v5 = 0.0
    v5 := na(v5[1]) ? sma(src, len) : (v5[1] * (len - 1) + src) / len   
    v6 = 2 * v2 - ema(v2, len)                                         
    v7 = 3 * (v2 - ema(v2, len)) + ema(ema(v2, len), len)               
    v8 = wma(2 * wma(src, len / 2) - wma(src, len), round(sqrt(len)))   
    v11 = sma(sma(src,len),len)                                        


    a1 = exp(-1.414*3.14159 / len)
    b1 = 2*a1*cos(1.414*3.14159 / len)
    c2 = b1
    c3 = (-a1)*a1
    c1 = 1 - c2 - c3
    v9 = 0.0
    v9 := c1*(src + nz(src[1])) / 2 + c2*nz(v9[1]) + c3*nz(v9[2])
    
    e = ema(v1, len)
    v10 = v1+(v1-e)
    
    type=="EMA"?v2 : type=="WMA"?v3 : type=="VWMA"?v4 : type=="SMMA"?v5 : type=="DEMA"?v6 : type=="TEMA"?v7 : type=="HullMA"?v8 : type=="SSMA"?v9 : type=="ZEMA"?v10 : type=="TMA"? v11: v1


ma_fast     = variant(typeFast, ma_src, lenFast)
ma_medium   = variant(typeMedium, ma_src, lenMedium)
ma_slow     = variant(typeSlow, ma_src, lenSlow)
ma_coloured = variant(typeColoured, srcColoured, lenColoured)

clrdirection = 1
clrdirection := rising(ma_coloured,2) ? 1 : falling(ma_coloured,2)? -1  : nz(clrdirection[1],1)


madirection  =  ma_fast>ma_medium and ma_medium>ma_slow? 1 : ma_fast<ma_medium and ma_medium<ma_slow? -1 : 0
madirection  := disableSlowMAFilter?  ma_fast>ma_medium? 1 : ma_fast<ma_medium? -1 : 0 : madirection
madirection  := disableMediumMAFilter?  ma_fast>ma_slow? 1 : ma_fast<ma_slow? -1 : 0 : madirection
madirection  := disableFastMAFilter?  ma_medium>ma_slow? 1 : ma_medium<ma_slow? -1 : 0 : madirection
madirection  := disableFastMAFilter and disableMediumMAFilter?  ma_coloured>ma_slow? 1 : -1 : madirection
madirection  := disableFastMAFilter and disableSlowMAFilter?    ma_coloured>ma_medium? 1 : -1 : madirection
madirection  := disableSlowMAFilter and disableMediumMAFilter?  ma_coloured>ma_fast? 1 : -1 : madirection


SUp=hl2-(SFactor*atr(SPd))
SDn=hl2+(SFactor*atr(SPd))

STrendUp   = 0.0
STrendDown = 0.0
STrendUp   := close[1]>STrendUp[1]? max(SUp,STrendUp[1]) : SUp
STrendDown := close[1]<STrendDown[1]? min(SDn,STrendDown[1]) : SDn
STrend = 0
STrend := close > STrendDown[1] ? 1: close< STrendUp[1]? -1: nz(STrend[1],1)
Tsl = STrend==1? STrendUp: STrendDown





noiupper =  abs(highestbars(oiLength))
noilower = abs(lowestbars(oiLength))



dcolour = madirection==1?green:madirection==-1?red:yellow
plotshape(hideTrendDirection? na : madirection, title="3xMA Trend Direction", location=location.bottom,style=shape.square, color=dcolour,transp=10)

_3xmabuy = 0
_3xmasell = 0
_3xmabuy  := clrdirection == 1 and close>ma_fast and madirection==1 ? nz(_3xmabuy[1])+1  : clrdirection == 1 and madirection==1?  nz(_3xmabuy[1])>0?nz(_3xmabuy[1])+1 : 0 : 0
_3xmasell := clrdirection ==-1 and close<ma_fast and madirection==-1 ? nz(_3xmasell[1])+1 : clrdirection ==-1 and madirection==-1? nz(_3xmasell[1])>0?nz(_3xmasell[1])+1 : 0 : 0
//

// SuperTrend Filtering
stbuy = 0
stsell = 0
stbuy  := clrdirection == 1 and STrend==1 ? nz(stbuy[1])+1  : 0
stsell := clrdirection ==-1 and STrend==-1 ? nz(stsell[1])+1 : 0


st3xmabuy = 0
st3xmasell = 0
st3xmabuy  := (disable3xMAFilter or _3xmabuy>0) and stbuy>0 ?  nz(st3xmabuy[1])+1  : 0
st3xmasell := (disable3xMAFilter or _3xmasell>0) and stsell>0 ? nz(st3xmasell[1])+1 : 0


oiMACrossbuy = 0
oiMACrosssell = 0
oi3xmabuy = 0
oi3xmasell = 0
oi3xmabuy  := oiMACrossbuy>0 and (disable3xMAFilter or madirection==1)? nz(oi3xmabuy[1])+1  : 0
oi3xmasell := oiMACrosssell>0 and (disable3xMAFilter or madirection==-1)? nz(oi3xmasell[1])+1 : 0


oistbuy = 0
oistsell = 0
oistbuy  := oiMACrossbuy>0 and STrend==1? nz(oistbuy[1])+1  : 0
oistsell := oiMACrosssell>0 and STrend==-1? nz(oistsell[1])+1 : 0


macrossSTbuy = 0
macrossSTsell = 0
macrossSTbuy  :=  crossover(ma_fast,ma_coloured) and STrend==1? nz(macrossSTbuy[1])+1  : 0
macrossSTsell :=  crossunder(ma_fast,ma_coloured) and STrend==-1? nz(macrossSTsell[1])+1 : 0


macross3xMAbuy = 0
macross3xMAsell = 0
macross3xMAbuy  :=  crossover(ma_fast,ma_coloured) and  (disable3xMAFilter or madirection==1) ? nz(macross3xMAbuy[1])+1  : 0
macross3xMAsell :=  crossunder(ma_fast,ma_coloured) and (disable3xMAFilter or madirection==-1) ? nz(macross3xMAsell[1])+1 : 0


long  = (u3xMATrendFilter and _3xmabuy==1) or (uSuperTrendFilter and stbuy==1) or (uBothTrendFilters and st3xmabuy==1) or 
          (uOI3xMAFilter and oi3xmabuy==1) or (uOISTFilter and oistbuy==1) or (uOIMACrossFilter and oiMACrossbuy==1) or 
          (uMACrossSTFilter and macrossSTbuy==1) or (uMACross3xMAFilter and macross3xMAbuy==1)
short = (u3xMATrendFilter and _3xmasell==1) or (uSuperTrendFilter and stsell==1) or (uBothTrendFilters and st3xmasell==1) or 
          (uOI3xMAFilter and oi3xmasell==1) or (uOISTFilter and oistsell==1) or (uOIMACrossFilter and oiMACrosssell==1) or
          (uMACrossSTFilter and macrossSTsell==1)  or (uMACross3xMAFilter and macross3xMAsell==1)


plotshape( long ? long : na, title="Long Line Marker", 
      location=location.belowbar,style=shape.arrowup, color=buyclr,transp=20,size=size.auto, text='▄▄▄▄▄▄▄▄▄▄▄▄',textcolor=buyclr)
plotshape( short ? short : na, title="Short Line Marker", 
      location=location.abovebar,style=shape.arrowdown, color=#f44336,transp=20,size=size.auto, text='▄▄▄▄▄▄▄▄▄▄▄▄',textcolor=sellclr)


hbuy = 0
hsell = 0
hbuy  := clrdirection == 1 ? nz(hbuy[1])+1  : 0
hsell := clrdirection ==-1 ? nz(hsell[1])+1 : 0

macrossbuy = 0
macrosssell = 0
macrossbuy  :=  crossover(ma_fast,ma_coloured)? nz(macrossbuy[1])+1  : 0
macrosssell :=  crossunder(ma_fast,ma_coloured)? nz(macrosssell[1])+1 : 0
//
along  = (disableAllFilters and hbuy==1) or (uMACrossFilter and macrossbuy==1) 
ashort = (disableAllFilters and hsell==1) or (uMACrossFilter and macrosssell==1)
plotarrow( along   ? 1 : ashort? -1 : na, title="ColouredMA or MACross Arrow", colorup=buyColour, colordown=sellColour, maxheight=100, minheight=50, transp=20)

long  := long or along
short := short or ashort

alertcondition(long or short, title="Signal Alert",message="Sniper enter")

//EOF